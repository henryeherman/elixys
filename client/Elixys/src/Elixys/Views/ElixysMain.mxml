<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:s="library://ns.adobe.com/flex/spark" xmlns:mx="library://ns.adobe.com/flex/mx"
		   xmlns:Views="Elixys.Views.*" creationComplete="OnCreationComplete()" width="100%" height="100%">
	<fx:Style source="Elixys.css" />
	<fx:Script>
		<![CDATA[
			import Elixys.Events.*;
			import Elixys.HTTP.*;
			import Elixys.Objects.*;
			
			import mx.controls.Alert;
			import mx.events.ResizeEvent;
			import mx.managers.PopUpManager;

			/***
			 *** Initialization functions
			 **/
			
			// Called when the object is created
			private function OnCreationComplete():void
			{
				// Add event listeners
				addEventListener(ResizeEvent.RESIZE, OnResize);
				_viewHome.addEventListener(HTTPRequestEvent.HTTPREQUEST, OnHTTPRequestEvent);
				_viewSelect.addEventListener(HTTPRequestEvent.HTTPREQUEST, OnHTTPRequestEvent);
				_viewView.addEventListener(HTTPRequestEvent.HTTPREQUEST, OnHTTPRequestEvent);

				// Set our connection to the AIR player
				SetAIRConnection(m_sAIRConnection);
			}

			// Step 1: This function is called by the container to set the server
			public function Init1_SetServer(sAIRConnection:String, sServer:String, sCredentials:String):void
			{
				// Set our connection to the AIR player
				SetAIRConnection(sAIRConnection);
				
				// Create the HTTP connection pool
				m_pHTTPConnectionPool = new HTTPConnectionPool(sServer, 80, 5);
				m_pHTTPConnectionPool.addEventListener(HTTPStatusEvent.HTTP_STATUS, OnHTTPStatusEvent);
				m_pHTTPConnectionPool.addEventListener(ProgressEvent.PROGRESS, OnHTTPProgressEvent);
				m_pHTTPConnectionPool.addEventListener(HTTPResponseEvent.HTTPRESPONSE, OnHTTPResponseEvent);
				m_pHTTPConnectionPool.addEventListener(ExceptionEvent.EXCEPTION, OnChildExceptionEvent);
				m_pHTTPConnectionPool.SetCredentials(sCredentials);

				// Load the system configuration
				m_pHTTPConnectionPool.SendRequestA("GET", "/Elixys/configuration", HTTPConnectionPool.MIME_JSON);
			}

			// Step 2: Called when the system configuration has been received
			public function Init2_OnConfiguration(pConfiguration:Configuration):void
			{
				// Remember the system configuration
				m_pServerConfiguration = pConfiguration;
				
				// Load the current state
				m_pHTTPConnectionPool.SendRequestA("GET", "/Elixys/state", HTTPConnectionPool.MIME_JSON);

				// Start the state timer
				m_pStateTimer = new Timer(1000);
				m_pStateTimer.addEventListener(TimerEvent.TIMER, OnStateTimer);
				m_pStateTimer.start();
			}

			/***
			 *** Member functions
			 **/
			
			// Set our connection to the AIR player
			private function SetAIRConnection(sAIRConnection:String):void
			{
				// Remember the connection to the AIR player
				m_sAIRConnection = sAIRConnection;
				
				// Do we have both a connection and a home view?
				if ((m_sAIRConnection != "") && (_viewHome != null))
				{
					// Yes, so create the local connection and tell the home view to display the logout button
					m_pAIRConnection = new LocalConnection();
					_viewHome.DisplayLogout(true);
					_viewHome.addEventListener(LogoutEvent.LOGOUT, OnLogoutEvent);
				}
			}
			

			private function UpdateState(pState:State):void
			{
				// Handle the pop up window first
				if (pState.ClientState() == StatePrompt.TYPE)
				{
					// Create and display the pop up window
					if (_viewPrompt == null)
					{
						_viewPrompt = new PromptView();
						_viewPrompt.addEventListener(HTTPRequestEvent.HTTPREQUEST, OnHTTPRequestEvent);
						PopUpManager.addPopUp(_viewPrompt, this, true);
					}

					// Update the pop up state
					_viewPrompt.UpdateState(pState);
					PopUpManager.centerPopUp(_viewPrompt);
					return;
				}
				else if (_viewPrompt != null)
				{
					// Remove the pop up window
					PopUpManager.removePopUp(_viewPrompt);
					_viewPrompt = null;
				}
				
				// Show and update the appropriate state
				switch (pState.ClientState())
				{
					case StateHome.TYPE:
						_viewStack.selectedChild = _viewHome;
						_viewHome.UpdateState(pState);
						break;
					
					case StateSelect.TYPE:
						_viewStack.selectedChild = _viewSelect;
						_viewSelect.UpdateState(pState);
						break;
					
					case StateView.TYPE:
						_viewStack.selectedChild = _viewView;
						_viewView.UpdateState(pState);
						break;
						
					default:
						Alert.show("Unknown client state received from server: " + pState.ClientState());
						break;
				}
			}
			
			/***
			 *** Callback functions
			 **/

			// Called when the application resizes
			private function OnResize(event:ResizeEvent):void
			{
				// Center the pop up window if one exists
				if (_viewPrompt != null)
				{
					PopUpManager.centerPopUp(_viewPrompt);
				}
			}

			// Called when another object wants to communicate over the HTTP connection
			private function OnHTTPRequestEvent(event:HTTPRequestEvent):void
			{
				// Send the request to the server
				m_pHTTPConnectionPool.SendRequestB(event.m_pHTTPRequest);
			}

			// Called when the status of the HTTP request is know
			private function OnHTTPStatusEvent(event:HTTPStatusEvent):void
			{
				// Notify the user of any error
				if (event.status != 200)
				{
					Alert.show("HTTP request failed: " + event.status);
				}
			}
			
			// Called as the HTTP download progresses
			private function OnHTTPProgressEvent(event:ProgressEvent):void
			{
			}
			
			// Called when the HTTP response is complete
			private function OnHTTPResponseEvent(event:HTTPResponseEvent):void
			{
				// Parse the JSON string
				var pHTTPResponse:HTTPResponse = event.m_pHTTPResponse;
				var sJSON:String = pHTTPResponse.m_pBody.readUTFBytes(pHTTPResponse.m_pBody.length);
				var pJSON:JSONObject = new JSONObject(sJSON);
				switch (pJSON.type)
				{
					case (Configuration.TYPE):
						// Continue initialization process
						var pConfiguration:Configuration = new Configuration(sJSON);
						Init2_OnConfiguration(pConfiguration);
						break;
						
					case (State.TYPE):
						// Update our state
						var pState:State = new State(sJSON);
						UpdateState(pState);
						break;
					
					case (Sequence.TYPE):
						// Update the active view
						var pSequence:Sequence = new Sequence(sJSON);
						(_viewStack.selectedChild as ViewBase).UpdateStateSequence(pSequence);
						break;
					
					case (Component.TYPE):
						// Update the active view
						var pComponent:Component = new Component(sJSON);
						(_viewStack.selectedChild as ViewBase).UpdateStateComponent(pComponent);
						break;
					
					case (Reagent.TYPE):
						// Update the active view
						var pReagent:Reagent = new Reagent(sJSON);
						(_viewStack.selectedChild as ViewBase).UpdateStateReagent(pReagent);
						break;
						
					case (ServerError.TYPE):
						// Determine the type of error
						var pServerError:ServerError = new ServerError(sJSON);
						if (pServerError.Description() == "State misalignment")
						{
							// The server is telling us that our request didn't make sense in the context of its copy of our state.  Ignore
							// this error and simply ask the server for the correct state
							m_pHTTPConnectionPool.SendRequestA("GET", "/Elixys/state", HTTPConnectionPool.MIME_JSON);
						}
						else
						{
							// Something went wrong on the server side.  Notify the user
							throw new Error("Server error: " + pServerError.Description());
						}
						break;

					default:
						// Unhandled server message
						throw new Error("Message type \"" + pJSON.type + "\" not handled");							
				}
			}
			
			// Called when an exception occurs in a child
			public function OnChildExceptionEvent(event:ExceptionEvent):void
			{
				// Send a log out event to the host
				OnLogoutEvent(new LogoutEvent(event.exception));
			}

			// Called at regular intervals to update the state
			private function OnStateTimer(event:TimerEvent):void
			{
				// Load the current state
				m_pHTTPConnectionPool.SendRequestA("GET", "/Elixys/state", HTTPConnectionPool.MIME_JSON);
			}
			
			// Called when the user logs out
			private function OnLogoutEvent(event:LogoutEvent):void
			{
				// Make sure we are logged in
				if (m_pHTTPConnectionPool != null)
				{
					// Do we have a connection to the AIR player?
					if ((m_pAIRConnection != null) && (m_sAIRConnection != ""))
					{
						// Yes, so send the log out notification and any error message
						m_pAIRConnection.send(m_sAIRConnection, "OnLogoutEvent", event.error);
					}
					else
					{
						// No, so display any error message
						if (event.error != "")
						{
							Alert.show(event.error, "Error");
						}
					}
					
					// Stop the state timer
					if (m_pStateTimer != null)
					{
						m_pStateTimer.stop();
					}
					
					// Clean up the connection pool
					m_pHTTPConnectionPool.DropAllConnections();
					m_pHTTPConnectionPool = null;
				}
			}

			// Local connection to the AIR player
			private var m_sAIRConnection:String = "";
			private var m_pAIRConnection:LocalConnection;
			
			// HTTP connection pool to the server
			private var m_pHTTPConnectionPool:HTTPConnectionPool;

			// State update timer
			private var m_pStateTimer:Timer;

			// Server configuration
			private var m_pServerConfiguration:Configuration;

			// Prompt pop up window
			private var _viewPrompt:PromptView;
		]]>
	</fx:Script>
	
	<mx:ViewStack id="_viewStack" creationPolicy="all" width="100%" height="100%">
		<Views:HomeView id="_viewHome" width="100%" height="100%" />
		<Views:SelectView id="_viewSelect" width="100%" height="100%" />
		<Views:ViewView id="_viewView" width="100%" height="100%" />
	</mx:ViewStack>
</mx:Canvas>
